buildscript {
  ext.kotlin_version = '1.2.21'

  repositories {
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.2' // Workaround for https://github.com/johnrengelman/shadow/issues/336
  }
}

plugins {
  id 'java'
  id 'application'
  id 'de.undercouch.download' version '3.3.0' // For the download tasks
  id 'eclipse' // Support for Eclipse projects
  id 'idea' // Support for IntelliJ IDEA projects
  id 'com.github.sherter.google-java-format' version '0.8'
}
//apply plugin: 'kotlin'

mainClassName = "org.memo.Toradocu"
apply plugin: 'com.github.johnrengelman.shadow' // Workaround for https://github.com/johnrengelman/shadow/issues/336

task downloadRandoop(type: Download) {
  src 'https://github.com/randoop/randoop/releases/download/v4.2.0/randoop-4.2.0.jar'
  dest 'lib/'
  onlyIfNewer true
  overwrite false
}

task downloadGlove(type: Download) {
  src 'http://star-rep.inf.usi.ch/alberto/glove/raw/master/glove-binary.zip'
  dest 'src/main/resources/glove-binary.zip'
  onlyIfNewer true
  overwrite false
}

task unzipGlove(dependsOn: downloadGlove, type: Copy) {
  def zipFile = file('src/main/resources/glove-binary.zip')
  def outputDir = file('src/main/resources')
  from zipTree(zipFile)
  into outputDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task downloadGloveModels(type: Download) {
  src 'http://star-rep.inf.usi.ch/alberto/glove/raw/master/glove-models.zip'
  dest 'src/main/resources/glove-models.zip'
  onlyIfNewer true
  overwrite false
}

task unzipGloveModels(dependsOn: downloadGloveModels, type: Copy) {
  def zipFile = file('src/main/resources/glove-models.zip')
  def outputDir = file('src/main/resources')
  from zipTree(zipFile)
  into outputDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

processResources.dependsOn(unzipGlove, unzipGloveModels)

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'
jar {
  manifest {
    attributes 'Implementation-Title': 'MeMo', 'Implementation-Version': version
  }
}

sourceSets {
  main {
    java {
      excludes = ['org/memo/util/old/*']
    }
  }
  test {
    java {
      excludes = ['org/memo/util/old*',
                  'org/memo/regression/*',
                  'org/memo/generator/**']
    }
    resources {
      exclude 'goal-output/issta_2018'
    }
  }
}

test {
  exclude 'org/memo/generator/**'
  exclude 'org/memo/accuracy/paper/*'
  reports {
    html.enabled = true
  }
  testLogging.showStandardStreams = true
  // Print standard output during tests execution.
  testLogging {
    events = ['STANDARD_OUT', 'FAILED']
    exceptionFormat = 'FULL'
  }
}

task issta18(type: Test) {
  include 'org/memo/accuracy/paper/*'
  enableAssertions = false
  ignoreFailures = true
}

task accuracyEQComplete(type: Test) {
  include 'org/memo/accuracy/equivalences/'
  enableAssertions = false
}

task accuracyEQ(type: Test) {
  include 'org/memo/accuracy/equivalences/dataset/'
  enableAssertions = false
}

task accuracyEQSB15(type: Test) {
  include 'org/memo/accuracy/equivalences/sbes15/'
  enableAssertions = false
}

task accuracyEQSB14(type: Test) {
  include 'org/memo/accuracy/equivalences/sbes14/'
  enableAssertions = false
}

task copyIssta2018GoalFiles(type: Copy) {
  def basePath = 'src/test/resources/goal-output'
  from(basePath) {
    include "commons-collections4-4.1/org.apache.commons.collections4.ArrayStack_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.bag.CollectionBag_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.bag.SynchronizedBag_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.BagUtils_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.ClosureUtils_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.CollectionUtils_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.comparators.FixedOrderComparator_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.FactoryUtils_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.FluentIterable_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.functors.AllPredicate_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.functors.AndPredicate_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.functors.AnyPredicate_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.functors.IfClosure_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.keyvalue.DefaultMapEntry_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.map.HashedMap_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.map.LazyMap_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.map.LRUMap_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.PredicateUtils_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.QueueUtils_goal.json"
    include "commons-collections4-4.1/org.apache.commons.collections4.trie.KeyAnalyzer_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.differentiation.DerivativeStructure_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.function.Gaussian_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.function.Logistic_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.function.StepFunction_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.FunctionUtils_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.integration.IterativeLegendreGaussIntegrator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.integration.SimpsonIntegrator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.interpolation.BivariateGridInterpolator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.interpolation.DividedDifferenceInterpolator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.interpolation.LinearInterpolator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.interpolation.LoessInterpolator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.polynomials.PolynomialFunctionNewtonForm_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.solvers.BracketingNthOrderBrentSolver_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.analysis.solvers.UnivariateSolverUtils_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.complex.Complex_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.distribution.LogisticDistribution_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.fitting.SimpleCurveFitter_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.fraction.BigFraction_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.fraction.Fraction_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.genetics.BinaryMutation_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.genetics.CycleCrossover_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.genetics.RandomKey_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.euclidean.oned.Interval_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.euclidean.oned.IntervalsSet_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.euclidean.threed.FieldRotation_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.euclidean.threed.Line_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.euclidean.threed.SubLine_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.euclidean.threed.Vector3D_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.partitioning.Region_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.spherical.twod.S2Point_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.geometry.Vector_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.linear.PreconditionedIterativeLinearSolver_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.linear.RealVector_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.linear.SymmLQ_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.ode.ContinuousOutputModel_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.ode.nonstiff.AdaptiveStepsizeIntegrator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.ode.nonstiff.EmbeddedRungeKuttaFieldIntegrator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.optimization.direct.AbstractSimplex_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.optimization.SimpleValueChecker_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.optimization.univariate.UnivariateMultiStartOptimizer_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.primes.Primes_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.random.BitsStreamGenerator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.random.HaltonSequenceGenerator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.random.RandomAdaptor_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.random.RandomDataGenerator_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.stat.clustering.DBSCANClusterer_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.stat.descriptive.rank.PSquarePercentile_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.stat.descriptive.summary.Sum_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.stat.descriptive.SummaryStatistics_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.util.FastMath_goal.json"
    include "commons-math3-3.6.1/org.apache.commons.math3.util.KthSelector_goal.json"
    include "gs-core-1.3/org.graphstream.graph.implementations.MultiGraph_goal.json"
    include "gs-core-1.3/org.graphstream.graph.implementations.SingleGraph_goal.json"
    include "gs-core-1.3/org.graphstream.graph.Path_goal.json"
    include "gs-core-1.3/org.graphstream.ui.graphicGraph.GraphicElement_goal.json"
    include "gs-core-1.3/org.graphstream.ui.swingViewer.util.GraphMetrics_goal.json"
    include "gs-core-1.3/org.graphstream.util.Environment_goal.json"
    include "gs-core-1.3/org.graphstream.util.set.FixedArrayList_goal.json"
    include "guava-19.0/com.google.common.base.CharMatcher_goal.json"
    include "guava-19.0/com.google.common.base.Converter_goal.json"
    include "guava-19.0/com.google.common.base.MoreObjects_goal.json"
    include "guava-19.0/com.google.common.base.Splitter_goal.json"
    include "guava-19.0/com.google.common.base.Strings_goal.json"
    include "guava-19.0/com.google.common.base.Throwables_goal.json"
    include "guava-19.0/com.google.common.base.Verify_goal.json"
    include "guava-19.0/com.google.common.cache.CacheLoader_goal.json"
    include "guava-19.0/com.google.common.collect.ArrayListMultimap_goal.json"
    include "guava-19.0/com.google.common.collect.ConcurrentHashMultiset_goal.json"
    include "guava-19.0/com.google.common.collect.ImmutableSortedMap_goal.json"
    include "guava-19.0/com.google.common.collect.ImmutableSortedMapFauxverideShim_goal.json"
    include "guava-19.0/com.google.common.collect.Iterators_goal.json"
    include "guava-19.0/com.google.common.escape.UnicodeEscaper_goal.json"
    include "guava-19.0/com.google.common.hash.BloomFilter_goal.json"
    include "guava-19.0/com.google.common.primitives.Doubles_goal.json"
    include "guava-19.0/com.google.common.primitives.Floats_goal.json"
    include "guava-19.0/com.google.common.primitives.Shorts_goal.json"
    include "guava-19.0/com.google.common.util.concurrent.AtomicDoubleArray_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.alg.AbstractPathElementList_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.alg.cycle.PatonCycleBase_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.alg.KShortestPaths_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.generate.CompleteGraphGenerator_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.generate.EmptyGraphGenerator_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.generate.LinearGraphGenerator_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.graph.AbstractGraph_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.graph.GraphDelegator_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.Graph_goal.json"
    include "jgrapht-core-0.9.2/org.jgrapht.Graphs_goal.json"
    include "plume-lib-1.1.0/plume.ArraysMDE_goal.json"
    include "plume-lib-1.1.0/plume.EntryReader.json"
    include "plume-lib-1.1.0/plume.EntryReader\$FlnReader_goal.json"
    include "plume-lib-1.1.0/plume.FuzzyFloat_goal.json"
    include "plume-lib-1.1.0/plume.MathMDE_goal.json"
    include "plume-lib-1.1.0/plume.RegexUtil_goal.json"
    include "plume-lib-1.1.0/plume.TimeLimitProcess_goal.json"
    include "plume-lib-1.1.0/plume.UtilMDE_goal.json"
    include "plume-lib-1.1.0/plume.WeakHasherMap_goal.json"
    include "plume-lib-1.1.0/plume.WeakIdentityHashMap_goal.json"
  }
  into "$basePath/issta_2018"
}

/* Pass Java system properties from the command line to test tasks. */
tasks.withType(Test) {
  systemProperties System.getProperties()
}

/* Create Javadoc documentation as part of build task. */
build.dependsOn javadoc
javadoc {
  options.encoding = 'UTF-8'
  exclude 'org/memo/generator/**'
  exclude 'org/memo/util/old/**'
}

/* Disable unneeded tasks. */
startScripts.enabled = false
distZip.enabled = false
distTar.enabled = false
assemble.enabled = false
// To exclude shadowJar task from build: ./gradlew build -PdisableShadowJar
if (project.hasProperty('disableShadowJar')) {
  shadowJar.enabled = false
}
startShadowScripts.enabled = false
shadowDistZip.enabled = false
shadowDistTar.enabled = false

compileJava {
  options.compilerArgs << '-Xlint:unchecked,deprecation'
  options.encoding = 'UTF-8'
}

repositories {
  mavenCentral()
}

dependencies {
  compile fileTree(dir: 'lib', include: '*.jar')
  def randoopJar = ["https://github.com/randoop/randoop/releases/download/v4.2.0/randoop-4.2.0.jar"]
                .collect{fileName->new File(fileName)}

  compile files{randoopJar}
  compile files("${System.properties['java.home']}/../lib/tools.jar")
  compile 'edu.stanford.nlp:stanford-corenlp:3.6.0'
  compile 'edu.stanford.nlp:stanford-corenlp:3.6.0:models-english'
  compile 'de.jungblut.math:tjungblut-math:1.3'
  compile 'de.jungblut.common:thomasjungblut-common:1.1'
  compile 'org.deeplearning4j:deeplearning4j-nlp:1.0.0-beta6'
  compile 'org.nd4j:nd4j-native-platform:1.0.0-beta6'
  compile 'org.jsoup:jsoup:1.8.3' // JSoup used only to remove HTML tags in comments.
  compile 'com.github.javaparser:javaparser-core:3.11.0'
  compile 'com.beust:jcommander:1.72'
  compile 'com.google.code.gson:gson:2.8.0'
  compile 'commons-io:commons-io:2.5'
  compile 'org.slf4j:slf4j-simple:1.7.21'
  compile 'org.apache.commons:commons-lang3:3.4'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile 'org.mdkt.compiler:InMemoryJavaCompiler:1.3.0'
  compile 'org.reflections:reflections:0.9.11'
  testCompile 'junit:junit:4.12'
  testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
}

// The following configuration excludes sfl4j->log4j binding that is added by de.jungblut.common
// dependency.
configurations.all {
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.3.1'
}

import de.undercouch.gradle.tasks.download.Download

/* Downloads binaries of the systems used to measure Toradocu's precision and recall. */
task downloadBinaries(type: Download) {
  src ([
    // commons-collections
    'http://repo1.maven.org/maven2/org/apache/commons/commons-collections4/4.1/commons-collections4-4.1.jar',
    // commons-math
    'http://repo1.maven.org/maven2/org/apache/commons/commons-math3/3.6.1/commons-math3-3.6.1.jar',
    // guava
    'http://repo1.maven.org/maven2/com/google/guava/guava/19.0/guava-19.0.jar',
    // jgrapht
    'http://repo1.maven.org/maven2/org/jgrapht/jgrapht-core/0.9.2/jgrapht-core-0.9.2.jar',
    // plume
    'https://github.com/mernst/plume-lib/releases/download/v1.1.0/plume-lib-1.1.0.tar.gz',
    // gs
    'http://repo1.maven.org/maven2/org/graphstream/gs-core/1.3/gs-core-1.3.jar',
    // weka
    'https://repo1.maven.org/maven2/nz/ac/waikato/cms/weka/weka-stable/3.8.0/weka-stable-3.8.0.jar',
    // hibernate
    'https://repo1.maven.org/maven2/org/hibernate/hibernate-core/5.4.2.Final/hibernate-core-5.4.2.Final.jar',
    // javax (for hibernate)
    'https://repo1.maven.org/maven2/javax/persistence/javax.persistence-api/2.2/javax.persistence-api-2.2.jar',
    // colt
    'https://repo1.maven.org/maven2/colt/colt/1.2.0/colt-1.2.0.jar',
    // joda (for elasticsearch)
    'https://repo1.maven.org/maven2/joda-time/joda-time/2.9.8/joda-time-2.9.8.jar',
    // lucene (for elasticsearch)
    'https://repo1.maven.org/maven2/org/apache/lucene/lucene-core/7.2.1/lucene-core-7.2.1.jar',
    // elasticsearch
    'https://repo1.maven.org/maven2/org/elasticsearch/elasticsearch/6.1.1/elasticsearch-6.1.1.jar',
    // gwt
    'https://repo1.maven.org/maven2/com/google/gwt/gwt-user/2.5.1/gwt-user-2.5.1.jar',
    // JDK8
    'http://star-rep.inf.usi.ch/arianna_blasi/jdk8-resources/raw/master/jdk8-2.9.0.jar'
  ])
  dest 'src/test/resources/bin'
  onlyIfNewer true
  overwrite false
}

/* Extracts binaries that were not already downloaded directly. */
task extractBinaries(type: Exec, dependsOn: 'downloadBinaries') {
  description "Extract .jar files from downloaded files"
  workingDir 'src/test/resources/bin'
  commandLine "bash", "-c", "tar xzf plume-lib-1.1.0.tar.gz --strip-components=2 plume-lib-1.1.0/java/plume.jar && mv plume.jar plume-lib-1.1.0.jar"
}

/* Downloads sources of the systems used to measure Toradocu's precision and recall. */
task downloadSources(type: Download) {
  src ([
    'https://archive.apache.org/dist/commons/collections/source/commons-collections4-4.1-src.zip',
    'https://archive.apache.org/dist/commons/math/source/commons-math3-3.6.1-src.zip',
    'http://repo1.maven.org/maven2/com/google/guava/guava/19.0/guava-19.0-sources.jar',
    'http://repo1.maven.org/maven2/org/jgrapht/jgrapht-core/0.9.2/jgrapht-core-0.9.2-sources.jar',
    'https://github.com/mernst/plume-lib/releases/download/v1.1.0/plume-lib-1.1.0.tar.gz',
    'http://repo1.maven.org/maven2/org/graphstream/gs-core/1.3/gs-core-1.3-sources.jar',
    'https://repo1.maven.org/maven2/org/hibernate/hibernate-core/5.4.2.Final/hibernate-core-5.4.2.Final-sources.jar',
    'https://repo1.maven.org/maven2/nz/ac/waikato/cms/weka/weka-stable/3.8.0/weka-stable-3.8.0-test-sources.jar',
    'https://repo1.maven.org/maven2/org/elasticsearch/elasticsearch/6.1.1/elasticsearch-6.1.1-sources.jar',
    'https://repo1.maven.org/maven2/com/google/gwt/gwt-user/2.5.1/gwt-user-2.5.1-sources.jar',
    'http://star-rep.inf.usi.ch/arianna_blasi/jdk8-resources/raw/master/jdk8-sources.zip',
    'https://github.com/kzn/colt/archive/refs/heads/master.zip'
    // 'https://repo1.maven.org/maven2/org/apache/lucene/lucene-core/7.2.1/lucene-core-7.2.1-sources.jar',
  ])
  dest 'src/test/resources/src'
  onlyIfNewer true
  overwrite false
}

/* Extracts source archives of the systems used to measure Toradocu's precision and recall. */
task extractSources(dependsOn: downloadSources) {
  doLast {
    file('src/test/resources/src').listFiles().findAll {
      it.name.endsWith('.zip') || it.name.endsWith('.tar.gz') || it.name.endsWith('.jar')
    }.each { file ->
      if (file.name.endsWith('.zip')) {
        copy {
          from zipTree(file)
          into 'src/test/resources/src/'
          fileMode 0664
        }
      } else if (file.name.endsWith('.tar.gz')) {
        copy {
          from tarTree(resources.gzip(file))
          into 'src/test/resources/src/'
          fileMode 0664
        }
      } else if (file.name.endsWith('.jar')) {
        def fileName = file.name
        def index = fileName.lastIndexOf('.')
        def fileBaseName = fileName.substring(0, index)
        copy {
          from zipTree(file)
          into 'src/test/resources/src/' + fileBaseName
          fileMode 0664
        }
      }
    }
  }
}

/* Workaround for the Javaparser bug: https://github.com/javaparser/javaparser/issues/990 */
task cleanSource(type: Exec, dependsOn: 'extractSources') {
    commandLine './clean.sh'
}

task removeTestOutputDirs(type: Delete) {
  delete 'build/test-results', 'aspects'
}

test.dependsOn removeTestOutputDirs, cleanSource, extractBinaries
issta18.dependsOn copyIssta2018GoalFiles, removeTestOutputDirs, cleanSource, extractBinaries

/* Make Emacs TAGS table */
task tags(type: Exec) {
  description "Run etags to create an Emacs TAGS table"
  environment PATH: "$System.env.PATH:$buildDir/utils/plume-lib/bin"
  commandLine "bash", "-c", "find src/ -path src/test/resources -prune -o -name '*.java' | sort-directory-order | xargs etags"
}

/* Install the git pre-commit hook */
task installGitHook(type: Copy) {
  from(new File(rootProject.rootDir, "git-pre-commit-hook.sh")) {
    rename({ return "pre-commit" })
  }
  into { new File(rootProject.rootDir, ".git/hooks") }
}
build.dependsOn(installGitHook)

build.dependsOn verifyGoogleJavaFormat
googleJavaFormat {
  exclude 'out'
  exclude 'src/test/resources'
  exclude 'src/main/resources/AspectTemplate.java'
}

shadowJar {
  // default: "${baseName}-${version}-${classifier}.${extension}",
  // where ${baseName} is the name of the directory into which the memo
  // repository is cloned.
  archiveName = "MeMo-${version}-${classifier}.${extension}"
  zip64 true
}

/* Downloads dependencies needed for the tutorial. */
task tutorial(type: Download) {
  src ([
    'http://search.maven.org/remotecontent?filepath=junit/junit/4.12/junit-4.12.jar',
    'http://search.maven.org/remotecontent?filepath=org/hamcrest/hamcrest-core/1.3/hamcrest-core-1.3.jar',
    'http://central.maven.org/maven2/org/aspectj/aspectjtools/1.8.9/aspectjtools-1.8.9.jar',
    'http://central.maven.org/maven2/org/aspectj/aspectjweaver/1.8.9/aspectjweaver-1.8.9.jar',
    'http://central.maven.org/maven2/org/aspectj/aspectjrt/1.8.9/aspectjrt-1.8.9.jar'
  ])
  dest 'tutorial'
  onlyIfNewer true
  overwrite false
}
tutorial.dependsOn shadowJar
